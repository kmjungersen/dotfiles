#!/usr/local/bin/python
# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/dotfiles

# Virtualenvwrapper stuff

# your project folder that we can `c [tab]` to
export PROJECTS=~/COS

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi


# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done


# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
# Make autoenv always work
cd .

# Virtualenv wrapper stuff, which I no longer use, but want to keep around for some reason...

# VIRTUALENVWRAPPER_PYTHON=/usr/local/bin/python
# export WORKON_HOME=~/.virtualenvs
# export PROJECT_HOME=WORKON_HOME
# source /usr/local/bin/virtualenvwrapper.sh
